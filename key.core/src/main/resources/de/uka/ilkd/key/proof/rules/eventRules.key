\schemaVariables {
    \modalOperator {diamond, box, diamond_transaction, box_transaction} #allmodal;
    \modalOperator {diamond, diamond_transaction} #diamond;
    \modalOperator {box, box_transaction} #box;
    \program SimpleExpression #se, #se0, #se1, #se2;
    \program MethodName #mn;
    \program SimpleExpression #seMn;
    \program [list] SimpleExpression #selist;
    \program [list] Expression #elist;
}

\rules(events:on, programRules:Java, runtimeExceptions:ban) {
  /*! We want to track that a method-call happened and also assign it an eventNr (see event.key). */
  methodCallToEvent {
       \schemaVar \formula post;
       \schemaVar \term Object rp;
       \schemaVar \term MethodName mn;
       \schemaVar \term Seq params;
       \find( ==> \modality{#allmodal}{.. #se.#mn(#selist); ...}\endmodality post)
       \varcond(\not \staticMethodReference(#se, #mn, #selist), \mayExpandMethod(#se, #mn, #selist), \getObject(rp, #se), \getMethodName(mn, #se, #mn, #selist), \getParams(params, #selist))
       "Normal Execution (#se != null)":
           \replacewith( ==> {\eventUpdate(event(rp, mn, params), eventNr)}{eventNr:=eventNr+1}\modality{#allmodal}{.. #method-call(#se.#mn(#selist)); ...}\endmodality (post));
           //\add( ==> #se = null);
       "Null Reference (#se = null)":
           \replacewith( ==> \modality{#allmodal}{.. throw new java.lang.NullPointerException();
                   ...}\endmodality (post))
           \add(#se = null ==>)
       \heuristics(update_apply)
  };

  /*! Removes an event update that is applied on a formula that does not contain an event. */
  simplifyEventUpdateOnFormula {
    \schemaVar \update u;
    \schemaVar \formula phi;
    \find({u}phi)
    \varcond(\isEventUpdate(u), \not \containsEvent(phi))
    \replacewith(phi)
    \heuristics(update_elim)
  };

  simplifyEventUpdateOnTerm {
    \schemaVar \update u;
    \schemaVar \term any t;
    \find({u}t)
    \varcond(\isEventUpdate(u), \not \containsEvent(t))
    \replacewith(t)
    \heuristics(update_elim)
  };

  simplifyEventUpdateOnPV {
    \schemaVar \update u;
    \schemaVar \program Variable pv;
    \find({u}pv)
    \varcond(\isEventUpdate(u))
    \replacewith(pv)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnPV {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \program Variable pv;
    \find({u || evtU}pv)
    \varcond(\isEventUpdate(evtU))
    \replacewith({u}pv)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnPV2 {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \program Variable pv;
    \find({evtU || u}pv)
    \varcond(\isEventUpdate(evtU))
    \replacewith({u}pv)
    \heuristics(update_elim)
  };

  simplifyEventUpdateOnHeap {
    \schemaVar \update u;
    \schemaVar \term Heap h;
    \find({u}h)
    \varcond(\isEventUpdate(u))
    \replacewith(h)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnHeap {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \term Heap h;
    \find({u || evtU}h)
    \varcond(\isEventUpdate(evtU))
    \replacewith({u}h)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnHeap2 {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \term Heap h;
    \find({evtU || u}h)
    \varcond(\isEventUpdate(evtU))
    \replacewith({u}h)
    \heuristics(update_elim)
  };

  simplifyEventUpdateOnField {
    \schemaVar \update u;
    \schemaVar \term Field f;
    \find({u}f)
    \varcond(\isEventUpdate(u))
    \replacewith(f)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnFormula {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \formula phi;
    \find({u || evtU}phi)
    \varcond(\isEventUpdate(evtU), \not \containsEvent(phi))
    \replacewith({u}phi)
    \heuristics(update_elim)
  };

  simplifyParallelEventUpdateOnFormula2 {
    \schemaVar \update u;
    \schemaVar \update evtU;
    \schemaVar \formula phi;
    \find({evtU || u}phi)
    \varcond(\isEventUpdate(evtU), \not \containsEvent(phi))
    \replacewith({u}phi)
    \heuristics(update_elim)
  };

  splitEventState {
    \schemaVar \term Event evtInUpdate;
    \schemaVar \term int evtNr;
    \schemaVar \term Event evt;
    \schemaVar \term boolean cond;
    \find({\eventUpdate(evtInUpdate, evtNr)}eventState(evt, cond))
    \replacewith(evtInUpdate=evt & cond = TRUE)
    /*
    "Event Condition (#cond true)":
        \replacewith(evtInUpdate=evt);
    "Event Condition (#cond false)":
        \replacewith(eventState(evt, cond))*/
    \heuristics(update_elim)
  };
}
